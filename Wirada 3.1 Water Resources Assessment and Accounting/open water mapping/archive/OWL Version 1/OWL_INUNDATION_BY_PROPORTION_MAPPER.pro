; ######################################################################
; NAME: OWL_INUNDATION_BY_PROPORTION_MAPPER.pro
; LANGUAGE: ENVI IDL
; AUTHOR: JUAN PABLO GUERSCHMAN & GARTH WARREN
; DATE: 06/11/2009
; DLM: 11/12/2009
; DESCRIPTION: THIS TOOL CREATES AN INUNDATION IMAGE BASED ON 
;              USER-SELECTED PARAMETERS. INUNDATION BY-PROPORTION IS
;              MAPPED BY-TIME-SERIES. A CELL IS CLASSIFIED AS BEING 
;              INUNDATED IF THE OWL VALUE IS EQ, GT, GE, LT OR LE A USER-
;              SELECTED OWL THRESHOLD AND IS EQ, GT, GE, LT OR LE A USER-
;              SELECTED PROPORTION THRESHOLD - WHERE PROPORTION IS THE
;              PERCENTAGE OF TIME INUNDATED, THAT IS TO SAY, THE 
;              PERCENTAGE OF, INUNDATION COUNT DIVIDED BY TOTAL COUNT
;              (NOT INCLUDING CLOUD DATES). IF THE INUNDATION CRITERIA
;              AND THE PROPORTION CRITERIA IS SATISFIED A CELL IS IS GIVEN
;              A VALUE OF 1, IF NOT, THE CELL IS GIVEN A VALUE OF 0.
; INPUT: ONE SINGLE-BAND IMAGE OR ONE MULTI-BAND IMAGE.
; OUTPUT: ONE IMAGE. 
; PARAMETERS: INPUT IS VIA WIDGETS.
; NOTES:
; ######################################################################
; 
PRO OWL_INUNDATION_BY_PROPORTION_MAPPER
  ; GET START TIME
  F_TIME = SYSTIME(1)
  PRINT,''
  PRINT,'BEGIN PROCESSING: OWL_INUNDATION_BY_PROPORTION_MAPPER'
  ;---------------------------------------------------------------------
  ; DEFINE THE INPUT IMAGE DATA (SINGLE BAND IMAGE OR MULTIBAND IMAGE)
  ENVI_SELECT, TITLE='SELECT INPUT IMAGE DATA', FID=FID, POS=POS
  ; CHECK WHETHER THE INPUT IMAGE DATA NAME AND PATH IS VALID
  IF (FID EQ -1) THEN BEGIN
    PRINT,''
    PRINT, 'THE SELECTED INPUT FILE IS NOT VALID'
    PRINT,''
    RETURN
  ENDIF
  ;---------------------------------------------------------------------
  ; COUNT THE NUMBER OF BANDS IN THE INPUT FILE
  ENVI_FILE_QUERY, FID, BNAME=BNAME, SNAME=SNAME, NB=NB, NS=NS, NL=NL, DATA_TYPE=DATA_TYPE
  RESULT = WHERE(BNAME, COUNT)
  BCOUNT = COUNT
  PRINT, 'NUMBER OF BANDS: ', BCOUNT
  ; GET DIMENSIONS
  DIMS = [-1, 0, NS-1, 0, NL-1]
  PRINT, 'IMAGE DIMENSIONS: ', DIMS
  ; GET MAP INFO
  MAP_INFO = ENVI_GET_MAP_INFO(FID=FID)
  ;---------------------------------------------------------------------
  ; SET OPERATOR
  OP_BASE = WIDGET_AUTO_BASE(TITLE='DEFINE...')
  LIST=['EQ','LE','LT','GE','GT']
  WO_OP = WIDGET_PMENU(OP_BASE, LIST=LIST, uvalue='OUTOP', $
    PROMPT='SELECT AN OPERATOR', XSIZE=20, /AUTO)  
  RESULT_OP = AUTO_WID_MNG(OP_BASE) 
  IF (RESULT_OP.ACCEPT EQ 0) THEN BEGIN
    PRINT,''
    PRINT, 'THE SELECTED OPERATOR IS NOT VALID'
    PRINT,''
    RETURN
  ENDIF ELSE BEGIN
    OPER = LIST(RESULT_OP.OUTOP)
    PRINT, 'OPERATOR: ', OPER
  ENDELSE
  ;---------------------------------------------------------------------
  ; SET PARAMETER
  PA_BASE = WIDGET_AUTO_BASE(TITLE='DEFINE...')
  WO_PA = WIDGET_PARAM(PA_BASE, DT=2, UVALUE='PARAM', $
    PROMPT='ENTER A PARAMETER', XSIZE=18, /AUTO)
  RESULT_PA = AUTO_WID_MNG(PA_BASE)
  IF (RESULT_PA.ACCEPT EQ 0) THEN BEGIN
    PRINT,''
    PRINT, 'THE SELECTED PARAMETER IS NOT VALID'
    PRINT,''
    RETURN
  ENDIF ELSE BEGIN
    PARAM = FIX(RESULT_PA.PARAM)
    PRINT, 'PARAMETER VALUE = ', STRTRIM(PARAM, 2)
  ENDELSE
  ;---------------------------------------------------------------------
  ; PRINT EXPRESSION
  PRINT,''
  PRINT, '  EXPRESSION:  (OWL ', OPER, ' ', STRTRIM(PARAM, 2),')'
  PRINT,''
  ;---------------------------------------------------------------------
  ; CREATE EMPTY ARRAY PART 1
  ACUM_OWL = LONARR(NS, NL)
  ACUM_OWL_NE255 = LONARR(NS, NL)
  ACUM_OWL_EQ255 = LONARR(NS, NL)
  ;---------------------------------------------------------------------
  ; LOOP THROUGH EACH BAND
  FOR i=0, BCOUNT-1 DO BEGIN ; START 'FOR i'
    ; GET START TIME FOR LOOP
    T_TIME = Systime(1)
    ;-------------------------------------------------------------------
    ; GET BAND DATA
    OWL_IN = ENVI_GET_DATA(FID=FID, POS=i, DIMS=DIMS)
    ;-------------------------------------------------------------------
    ; ADD TO THE ARRAY IF THE CELL MEETS THE EXPRESSION CRITERIA AT THIS 
    ; BAND/DATE COMBINATION. AFTER THE LAST DATE/BAND THE ARRAY ACUM_OWL 
    ; IS MADE-UP OF CELLS THAT HAVE MET THE CRITERIA AT LEAST ONCE DURING 
    ; THE TIME-SERIES.
    IF (OPER EQ 'EQ') AND (PARAM NE 255) THEN ACUM_OWL += ((OWL_IN EQ STRTRIM(PARAM, 2)) AND (OWL_IN NE 255))
    IF OPER EQ 'LE' THEN ACUM_OWL += ((OWL_IN LE STRTRIM(PARAM, 2)) AND (OWL_IN NE 255))
    IF OPER EQ 'LT' THEN ACUM_OWL += ((OWL_IN LT STRTRIM(PARAM, 2)) AND (OWL_IN NE 255))
    IF OPER EQ 'GE' THEN ACUM_OWL += ((OWL_IN GE STRTRIM(PARAM, 2)) AND (OWL_IN NE 255))
    IF OPER EQ 'GT' THEN ACUM_OWL += ((OWL_IN GT STRTRIM(PARAM, 2)) AND (OWL_IN NE 255))
    ;-------------------------------------------------------------------
    ; ACCUMULATION OF VALUES NOT EQUAL TO 255
    ACUM_OWL_NE255 += (OWL_IN NE 255)
    ; ACCUMULATION OF VALUES EQUAL TO 255
    ACUM_OWL_EQ255 += (OWL_IN EQ 255)
    ;-------------------------------------------------------------------
    ; PRINT THE LOOP PROCESSING TIME
    SECONDS = (SYSTIME(1)-T_TIME)
    PRINT,''
    PRINT, SECONDS,'  SECONDS FOR BAND ', i+1, ' OF ', BCOUNT
    ;-------------------------------------------------------------------
  ENDFOR ; END 'FOR i' ROI
  ;---------------------------------------------------------------------
  ; SET VALUE '1.0' TO ALL ELEMENTS IN 'FREQ_OWL_NE255'
  ACUM_OWL_NE255 *= 1.0
  ACUM_OWL_ALL = (ACUM_OWL_EQ255 + ACUM_OWL_NE255)*1.0
  ;---------------------------------------------------------------------
  ; GET PERCENTAGE OF
  IF (OPER EQ 'EQ') THEN BEGIN 
    IF (PARAM EQ 255) THEN OWL_EXP = ((ACUM_OWL_EQ255/ACUM_OWL_ALL)*100)
    IF (PARAM NE 255) THEN OWL_EXP = ((ACUM_OWL/ACUM_OWL_NE255)*100)
  ENDIF ELSE BEGIN
    OWL_EXP = ((ACUM_OWL/ACUM_OWL_NE255)*100)
  ENDELSE
  ;---------------------------------------------------------------------
  ; CREATE EMPTY ARRAY
  FREQ_OWL = LONARR(NS, NL)
  OUT = LONARR(NS, NL) 
  ;---------------------------------------------------------------------
  ; DEFINE OPERATOR 2 (% OF TIME)
  OP_BASE2 = WIDGET_AUTO_BASE(TITLE='DEFINE...')
  LIST=['EQ','LE','LT','GE','GT']
  WO_OP2 = WIDGET_PMENU(OP_BASE2, LIST=LIST, uvalue='OUTOP2', $
    PROMPT='SELECT AN OPERATOR FOR INUNDATION PROPORTION', XSIZE=20, /AUTO)  
  RESULT_OP2 = AUTO_WID_MNG(OP_BASE2) 
  IF (RESULT_OP2.ACCEPT EQ 0) THEN BEGIN
    PRINT,''
    PRINT, 'THE SELECTED OPERATOR IS NOT VALID'
    PRINT,''
    RETURN
  ENDIF ELSE BEGIN
    OPER2 = LIST(RESULT_OP2.OUTOP2)
    PRINT,''
    PRINT, 'OPERATOR: ', OPER2
  ENDELSE
  ;---------------------------------------------------------------------
  ; SET PARAMETER
  PA_BASE2 = WIDGET_AUTO_BASE(TITLE='DEFINE...')
  WO_PA2 = WIDGET_PARAM(PA_BASE2, DT=2, UVALUE='PARAM2', $
    PROMPT='ENTER A PARAMETER (INUNDATION PROPORTION)', XSIZE=18, /AUTO)
  RESULT_PA2 = AUTO_WID_MNG(PA_BASE2)
  IF (RESULT_PA2.ACCEPT EQ 0) THEN BEGIN
    PRINT,''
    PRINT, 'THE SELECTED PARAMETER IS NOT VALID'
    PRINT,''
    RETURN
  ENDIF ELSE BEGIN
    PARAM2 = FIX(RESULT_PA2.PARAM2)
    PRINT,''
    PRINT, 'PARAMETER VALUE = ', STRTRIM(PARAM2, 2)
  ENDELSE
  ;---------------------------------------------------------------------
  ; PRINT EXPRESSION
  PRINT, '  EXPRESSION:  (OWL ', OPER2, ' ', STRTRIM(PARAM2, 2),')'
  PRINT,''
  ;---------------------------------------------------------------------  
  ; DEFINE OUTPUT FILE
  BASE = WIDGET_AUTO_BASE(TITLE='SELECT OUTPUT FILE')
  WO = WIDGET_OUTFM(BASE, UVALUE='OUTFILE1', /AUTO) 
  RESULT_OUT1 = AUTO_WID_MNG(BASE)
  IF (RESULT_OUT1.ACCEPT EQ 0) THEN BEGIN
    PRINT,''
    PRINT, 'THE SELECTED OUTPUT FILE IS NOT VALID'
    PRINT,''
    RETURN
  ENDIF
  IF ((RESULT_OUT1.OUTFILE1.IN_MEMORY) EQ 1) THEN $
    PRINT, 'OUTPUT TO MEMORY' ELSE PRINT, 'SELECTED OUTPUT FILE: ', RESULT_OUT1.OUTFILE1.NAME
  ;---------------------------------------------------------------------
  ; GET PERCENT OF FREQUENCY RESULTS
  IF OPER2 EQ 'EQ' THEN FREQ_OWL += (OWL_EXP EQ STRTRIM(PARAM2, 2))
  IF OPER2 EQ 'LE' THEN FREQ_OWL += (OWL_EXP LE STRTRIM(PARAM2, 2))
  IF OPER2 EQ 'LT' THEN FREQ_OWL += (OWL_EXP LT STRTRIM(PARAM2, 2))
  IF OPER2 EQ 'GE' THEN FREQ_OWL += (OWL_EXP GE STRTRIM(PARAM2, 2))
  IF OPER2 EQ 'GT' THEN FREQ_OWL += (OWL_EXP GT STRTRIM(PARAM2, 2))
  ;---------------------------------------------------------------------
  ; GET RESULTS
  OUT += (FREQ_OWL GE 1)
  OWL_EXP2 = FIX(OUT)
  ;---------------------------------------------------------------------
  ; SAVE DATA TO OUTPUT
  FNAME1 = RESULT_OUT1.OUTFILE1.NAME
  ENVI_WRITE_ENVI_FILE, OWL_EXP2, OUT_NAME=FNAME1, MAP_INFO=MAP_INFO, /NO_OPEN
  ;---------------------------------------------------------------------
  PRINT,''
  ; PRINT THE PROCESSING TIME
  MINUTES = (SYSTIME(1)-F_TIME)/60
  PRINT, MINUTES,'  MINUTES'
  PRINT,''
  PRINT,'FINISHED PROCESSING: OWL_INUNDATION_BY_PROPORTION_MAPPER'
  PRINT,'' 
END  