; ######################################################################
; NAME: OWL_INUNDATION_COUNT_MAPPER.pro
; LANGUAGE: ENVI IDL
; AUTHOR: JUAN PABLO GUERSCHMAN & GARTH WARREN
; DATE: 10/12/2009
; DLM: 11/12/2009
; DESCRIPTION: THIS TOOL CREATES AN INUNDATION COUNT IMAGE BASED ON USER 
;              SELECTED PARAMETERS. INUNDATION COUNT IS MAPPED BY-TIME-
;              SERIES. COUNT IS THE NUMBER OF TIMES THAT THE USER-SELECTED
;              THRESHOLD WAS SATISFIED DURING THE TIME-SERIES. FOR EXAMPLE,
;              SAY THE USER-SELECTED INUNDATION THRESHOLD IS SET AS GE50.
;              SAY A GIVEN CELL HAS AN OUTPUT VALUE OF 31. THIS MEANS THAT 
;              AT THAT PARTICULAR CELL LOCATION OWL WAS EQUAL TO, OR GREATER
;              THAT 50 A TOTAL OF 31 TIMES.
; INPUT: ONE SINGLE-BAND IMAGE OR ONE MULTI-BAND IMAGE.
; OUTPUT: ONE COUNT IMAGE. 
; PARAMETERS: INPUT IS VIA WIDGETS.
; NOTES:
; ######################################################################
; 
PRO OWL_INUNDATION_COUNT_MAPPER
  ; GET START TIME
  F_TIME = SYSTIME(1)
  PRINT,''
  PRINT,'BEGIN PROCESSING: OWL_INUNDATION_COUNT_MAPPER'
  ;---------------------------------------------------------------------
  ; DEFINE THE INPUT IMAGE DATA (SINGLE BAND IMAGE OR MULTIBAND IMAGE)
  ENVI_SELECT, TITLE='SELECT INPUT IMAGE DATA', FID=FID, POS=POS
  ; CHECK WHETHER THE INPUT IMAGE DATA NAME AND PATH IS VALID
  IF (FID EQ -1) THEN BEGIN
    PRINT,''
    PRINT, 'THE SELECTED INPUT FILE IS NOT VALID'
    PRINT,''
    RETURN
  ENDIF
  ENVI_FILE_QUERY, FID, BNAME=BNAME, SNAME=SNAME, NB=NB, NS=NS, NL=NL, DATA_TYPE=DATA_TYPE
  ;---------------------------------------------------------------------
  ; COUNT THE NUMBER OF BANDS IN THE INPUT FILE
  RESULT = WHERE(BNAME, COUNT)
  BCOUNT = COUNT
  PRINT, 'NUMBER OF BANDS: ', BCOUNT
  ; GET DIMENSIONS
  DIMS = [-1, 0, NS-1, 0, NL-1]
  PRINT, 'IMAGE DIMENSIONS: ', DIMS
  ; GET MAP INFO
  MAP_INFO = ENVI_GET_MAP_INFO(FID=FID)
  ;---------------------------------------------------------------------
  ; DEFINE OPERATOR: WIDGET_STRING
  OP_BASE = WIDGET_AUTO_BASE(TITLE='DEFINE...')
  LIST=['EQ','LE','LT','GE','GT']
  WO_OP = WIDGET_PMENU(OP_BASE, LIST=LIST, uvalue='OUTOP', $
    PROMPT='SELECT AN OPERATOR', XSIZE=20, /AUTO)  
  RESULT_OP = AUTO_WID_MNG(OP_BASE) 
  IF (RESULT_OP.ACCEPT EQ 0) THEN BEGIN
    PRINT,''
    PRINT, 'THE SELECTED OPERATOR IS NOT VALID'
    PRINT,''
    RETURN
  ENDIF ELSE BEGIN
    OPER = LIST(RESULT_OP.OUTOP)
    PRINT, 'OPERATOR: ', OPER
  ENDELSE
  ;---------------------------------------------------------------------
  ; SET PARAMETER IN OPERATOR: WIDGET_PARAM
  PA_BASE = WIDGET_AUTO_BASE(TITLE='DEFINE...')
  WO_PA = WIDGET_PARAM(PA_BASE, DT=2, UVALUE='PARAM', $
    PROMPT='ENTER A PARAMETER', XSIZE=18, /AUTO)
  RESULT_PA = AUTO_WID_MNG(PA_BASE)
  IF (RESULT_PA.ACCEPT EQ 0) THEN BEGIN
    PRINT,''
    PRINT, 'THE SELECTED PARAMETER IS NOT VALID'
    PRINT,''
    RETURN
  ENDIF ELSE BEGIN
    PARAM = FIX(RESULT_PA.PARAM)
    PRINT, 'PARAMETER VALUE = ', STRTRIM(PARAM, 2)
  ENDELSE
  ;---------------------------------------------------------------------
  ; PRINT EXPRESSION
  PRINT,''
  PRINT, '  EXPRESSION:  (OWL ', OPER, ' ', STRTRIM(PARAM, 2),')'
  PRINT,''
  ;---------------------------------------------------------------------
  ; SET THE OUTPUT FILE
  BASE = WIDGET_AUTO_BASE(TITLE='SELECT OUTPUT FILE')
  WO = WIDGET_OUTFM(BASE, UVALUE='OUTFILE1', /AUTO) 
  RESULT_OUT1 = AUTO_WID_MNG(BASE)
  IF (RESULT_OUT1.ACCEPT EQ 0) THEN BEGIN
    PRINT,''
    PRINT, 'THE SELECTED OUTPUT FILE IS NOT VALID'
    PRINT,''
    RETURN
  ENDIF
  IF ((RESULT_OUT1.OUTFILE1.IN_MEMORY) EQ 1) THEN $
    PRINT, 'OUTPUT TO MEMORY' ELSE PRINT, 'SELECTED OUTPUT FILE: ', RESULT_OUT1.OUTFILE1.NAME
  ;---------------------------------------------------------------------
  ; CREATE EMPTY ARRAY
  ACUM_OWL = LONARR(NS, NL)
  ;---------------------------------------------------------------------
  ; LOOP THROUGH EACH BAND
  FOR i=0, BCOUNT-1 DO BEGIN ; START 'FOR i'
    ; GET START TIME FOR LOOP
    T_TIME = Systime(1)
    ;-------------------------------------------------------------------
    ; GET BAND DATA
    OWL_IN = ENVI_GET_DATA(FID=FID, POS=i, DIMS=DIMS)
    ;-------------------------------------------------------------------
    ; ADD TO THE ARRAY IF THE CELL MEETS THE EXPRESSION CRITERIA AT THIS 
    ; BAND/DATE COMBINATION. AFTER THE LAST DATE/BAND THE ARRAY ACUM_OWL 
    ; IS MADE-UP OF CELLS THAT HAVE MET THE CRITERIA AT LEAST ONCE DURING 
    ; THE TIME-SERIES.
    IF (OPER EQ 'EQ') AND (PARAM EQ 255) THEN ACUM_OWL += OWL_IN EQ STRTRIM(PARAM, 2)
    IF (OPER EQ 'EQ') AND (PARAM NE 255) THEN ACUM_OWL += ((OWL_IN EQ STRTRIM(PARAM, 2)) AND (OWL_IN NE 255))
    IF OPER EQ 'LE' THEN ACUM_OWL += ((OWL_IN LE STRTRIM(PARAM, 2)) AND (OWL_IN NE 255))
    IF OPER EQ 'LT' THEN ACUM_OWL += ((OWL_IN LT STRTRIM(PARAM, 2)) AND (OWL_IN NE 255))
    IF OPER EQ 'GE' THEN ACUM_OWL += ((OWL_IN GE STRTRIM(PARAM, 2)) AND (OWL_IN NE 255))
    IF OPER EQ 'GT' THEN ACUM_OWL += ((OWL_IN GT STRTRIM(PARAM, 2)) AND (OWL_IN NE 255))
    ;-------------------------------------------------------------------
    ; PRINT THE LOOP PROCESSING TIME
    SECONDS = (SYSTIME(1)-T_TIME)
    PRINT,''
    PRINT, SECONDS,'  SECONDS FOR BAND ', i+1, ' OF ', BCOUNT
    ;-------------------------------------------------------------------
  ENDFOR ; END 'FOR i' ROI
  ;---------------------------------------------------------------------
  ; RENAME OUTPUT
  OWL_EXP = ACUM_OWL
  ;---------------------------------------------------------------------  
  ; SAVE DATA TO OUTPUT
  FNAME = RESULT_OUT1.OUTFILE1.NAME
  ENVI_WRITE_ENVI_FILE, OWL_EXP, OUT_NAME=FNAME, MAP_INFO=MAP_INFO, /NO_OPEN
  ;---------------------------------------------------------------------
  ; PRINT THE PROCESSING TIME
  PRINT,''
  MINUTES = (SYSTIME(1)-F_TIME)/60
  PRINT, MINUTES,'  MINUTES'
  PRINT,''
  PRINT,'FINISHED PROCESSING: OWL_INUNDATION_COUNT_MAPPER'
  PRINT,'' 
END  